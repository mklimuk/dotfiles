#!/usr/bin/env zsh
#
fpath=($ZDOTDIR/plugins $fpath)

# Download Zinit, if it's not there yet
if [ ! -d "$ZINIT_HOME" ]; then
   mkdir -p "$(dirname $ZINIT_HOME)"
   git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

# Source/Load zinit
source "${ZINIT_HOME}/zinit.zsh"

# prompt
#eval "$(starship init zsh)"
if [ "$TERM_PROGRAM" != "Apple_Terminal" ]; then
  eval "$(oh-my-posh init zsh --config $HOME/.config/ohmyposh/config.toml)"
fi

# install zsh plugins using zinit turbo mode
# fzf-tab adds an fzf based completion menu
zinit wait lucid light-mode for \
  atinit"zicompinit; zicdreplay" \
      zdharma-continuum/fast-syntax-highlighting \
      Aloxaf/fzf-tab \
      Tarrasch/zsh-bd \
      peterhurford/git-it-on.zsh \
  atload"_zsh_autosuggest_start" \
      zsh-users/zsh-autosuggestions \
  blockf atpull'zinit creinstall -q .' \
      zsh-users/zsh-completions

# snippets
zinit snippet OMZL::git.zsh                 # this is necessary if we want to use oh-my-zsh plugins
zinit snippet OMZP::git                     # git plugin (TODO: select aliases I really want to use)
#zinit snippet OMZP::ubuntu                  # TODO: move to linux and filter out stuff I really want
zinit snippet OMZP::golang
zinit snippet OMZP::command-not-found       # this proposes packages that contain the command that was not found

# Load completions
autoload -Uz compinit && compinit

zinit cdreplay -q

# navigation options
setopt AUTO_CD              # go to folder path without using cd
setopt AUTO_PUSHD           # push the old directory onto the stack on cd
setopt PUSHD_IGNORE_DUPS    # do not store duplicates in the stack
setopt PUSHD_SILENT         # do not print the directory stack after pushd or popd
setopt CORRECT              # apelling correction
setopt CDABLE_VARS          # change directory to a path stored in a variable
setopt EXTENDED_GLOB        # use extended globbing syntax

# history options
setopt APPEND_HISTORY            # this appends history file instead of replacing; allows all session to share history
setopt EXTENDED_HISTORY          # write the history file in the ':start:elapsed;command' format
setopt SHARE_HISTORY             # share history between all sessions
setopt HIST_EXPIRE_DUPS_FIRST    # expire a duplicate event first when trimming history
setopt HIST_IGNORE_DUPS          # do not record an event that was just recorded again
setopt HIST_IGNORE_ALL_DUPS      # delete an old recorded event if a new event is a duplicate
setopt HIST_FIND_NO_DUPS         # do not display a previously found event
setopt HIST_IGNORE_SPACE         # do not record an event starting with a space
setopt HIST_SAVE_NO_DUPS         # do not write a duplicate event to the history file
setopt HIST_VERIFY               # do not execute immediately upon history expansion

# moved here because it was overriden when put in .zshenv
export HISTFILE=$HOME/.zsh_history

# aliases
source "$ZDOTDIR"/aliases.zsh

# utility scripts
source "$ZDOTDIR"/scripts.zsh

# completion
source "$ZDOTDIR"/completion.zsh

function _new_command {
    zle push-input
    BUFFER=""
}

zle -N _new_command

# Keybindings
bindkey '^p' history-search-backward
bindkey '^n' history-search-forward
bindkey '^[w' kill-region

# macbook keyboard home
bindkey  '^[[H'   .beginning-of-line
bindkey  '^[[F'   .end-of-line

# PATH setup - prioritize Apple Silicon Homebrew
# Remove any existing Homebrew paths first, then add Apple Silicon Homebrew at the beginning
export PATH=$(echo $PATH | tr ':' '\n' | grep -v '/usr/local/bin\|/usr/local/sbin\|/opt/homebrew/bin\|/opt/homebrew/sbin' | tr '\n' ':' | sed 's/:$//')
export PATH=/opt/homebrew/bin:/opt/homebrew/sbin:$PATH:$HOME/.cargo/bin:$HOME/.local/bin:$HOME/.npm-global/bin

# shell integrations
# Force Apple Silicon Homebrew for shell environment
if [[ -f /opt/homebrew/bin/brew ]]; then
  eval "$(/opt/homebrew/bin/brew shellenv)"
elif command -v brew > /dev/null; then
  eval "$(brew shellenv)"
fi

eval "$(fzf --zsh)"
eval "$(zoxide init --cmd cd zsh)"
# TODO: figure out the way to use atuin
#eval "$(atuin init zsh)"

# os custom imports
if [[ $(uname) == "Darwin" ]]; then
    source "$ZDOTDIR"/macos.zsh
elif command -v apt > /dev/null; then
    source "$ZDOTDIR"/linux.zsh
else
    echo 'Unknown OS!'
fi

if command -v go > /dev/null; then
    # golang
    go env -w GOPROXY="https://goproxy.io,direct"
    go env -w GOPRIVATE="github.com/gophertribe/*,github.com/mklimuk/*,github.com/satsysoft/*"

    #export GOCACHE="$XDG_CACHE_HOME/go-build"

    GOBIN="$(go env GOPATH)/bin:$(go env GOROOT)/bin"
    export PATH=$PATH:$GOBIN
fi

# command specific imports
if command -v systemctl > /dev/null; then
    source "$ZDOTDIR"/systemd.zsh
fi

# kubernetes settings
if command -v kubectl > /dev/null; then
    source "$ZDOTDIR"/kubernetes.zsh
fi
